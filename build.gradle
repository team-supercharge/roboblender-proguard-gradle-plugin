group 'io.supercharge'
version '1.0-SNAPSHOT'

ext {
    artifactId = 'roboblender-proguard-gradle-plugin'
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile "com.github.javaparser:javaparser-core:2.1.0"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from groovydoc
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty("signing.keyId")) {
    signing {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment { deployment -> signing.signPom(deployment) }

            if (project.hasProperty('sonatypeUsername')) {
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }

            uniqueVersion = true

            pom.project {

                parent {
                    groupId 'org.sonatype.oss'
                    artifactId 'oss-parent'
                    version '7'
                }

                name 'RoboBlender Proguard Gradle plugin'
                packaging 'jar'
                description 'A plugin which can automatically generate Proguard rule for the RoboBlender annotation database.'
                url 'https://github.com/team-supercharge/roboblender-proguard-gradle-plugin'

                scm {
                    url 'https://github.com/team-supercharge/roboblender-proguard-gradle-plugin'
                    connection 'scm:git:git@github.com:team-supercharge/roboblender-proguard-gradle-plugin.git'
                    developerConnection 'scm:git:git@github.com:team-supercharge/roboblender-proguard-gradle-plugin.git'
                }

                licenses {
                    license {
                        name 'Apache 2'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'WonderCsabo'
                        name 'Csaba Koz√°k'
                        email 'csaba.kozak@supercharge.io'
                        organization 'Supercharge'
                        organizationUrl 'http://supercharge.io'
                    }
                }
            }
        }
    }
}
